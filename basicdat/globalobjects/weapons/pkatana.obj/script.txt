//Show its poisoned if its identified
IfNameIsKnown
  tmpargument = 0	//Is it not changed already?
  IfArmorIs
    tmpargument = 1	//Then show poison icon
    ChangeArmor

IfSpawned				//
  tmpargument = ACTIONJB		  //
  DoAction				  //
  KeepAction				  //
  MakeCrushValid
IfCrushed
  tmpx = selfx
  tmpy = selfy
  tmpdistance = selfz
  tmpargument = 1
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  GoPoof
  tmpargument = 1
  SendMessageNear
IfDropped				// Make it lie on floor
  KeepAction				  //
IfGrabbed				// Tell them what they've won...
  SetTargetToWhoeverIsHolding		  //
  IfTargetIsAPlayer			  //
    tmpargument = 0			  //
    SendMessageNear			  //
IfHitGround				// Make a sound
  tmpargument = 1			  //
  PlaySound				  //
IfNotDropped
  tmpargument = 2
  SendMessageNear
IfTakenOut
  tmpargument = 2
  PlaySound
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 4
    SendMessageNear
//IfNotPutAway
//  tmpargument = 3         //No longer needed, this is handled by the sourcecode now...
//  SendMessageNear

//Check if user poisoned himself
IfUsed
  SetTargetToWhoeverIsHolding
  tmpargument = [POIS]
  IfTargetHasSkillID		//Does it have poison use skill?
    IfTargetIsAPlayer
      MakeNameKnown		//Tell the players its poisoned
  Else
    tmpx = rand % 4096 + 1024 	//Wisdom determines the chance, 20 is never and 4 is almost always
    tmpy = targetwis
    IfXIsMoreThanY
      SetOwnerToTarget
      EnchantTarget		//Give the character some poison damage
      tmpargument = 5
      SendMessageNear		//Tell them what happened
      DetachFromHolder		//Drop the weapon
      IfTargetIsAPlayer
        MakeNameKnown		//Tell the players its poisoned


//Guys with a conduct dont use poisoned weapons
IfGrabbed
  SetTargetToWhoeverIsHolding
  tmpargument = [CODE]
  IfTargetHasSkillID
    tmpargument = 6		//I refuse to hold this!
    SendMessageNear
    MakeNameKnown		//Its poisoned, yes
    DetachFromHolder

End					// All done
