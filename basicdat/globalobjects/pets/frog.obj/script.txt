//Jump when the target is higher than self
IfHeld
  DoNothing
Else
  tmpx = selfz + 100
  tmpy = targetz
  IfXIsLessThanY			//Make sure he is a lot higher than self
    tmpx = 200
    tmpy = targetaltitude
    IfYIsLessThanX			//Make sure he isnt jumping or flying
      tmpargument = LATCHJUMP
      PressLatchButton			//Jump!

//Sound when hit ground
IfHitGround
  tmpargument = 2
  PlaySound
  
//This makes poison stronger with each level up
IfScoredAHit
  tmpargument = 0       // Cost owner 0 mana per second
  tmpdistance = 0        // Give owner 0 life per second
  tmpx = 0               // Give target 0 mana per second
  tmpy = 1+selflevel*26  // Cost target 0.1 life per second
  tmpx = 0 - tmpx        // for each level the frog is
  SetEnchantBoostValues

// ZZ> This function makes the character wanders around its enemy
IfSpawned
  MakeCrushValid
  SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      SetOwnerToTarget
      JoinTargetTeam
      MakeNameKnown

IfGrabbed
  MakeCrushInvalid
  IfNameIsKnown
    SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      SetOldTarget
      SetTargetToOwner
      IfTargetIsOldTarget
        tmpargument = 5
      Else
        tmpargument = 6
      SendMessageNear
  Else
    SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      SetOwnerToTarget
      JoinTargetTeam
      MakeNameKnown
      tmpargument = 3
      SendMessageNear
      tmpargument = 4
      SendMessageNear
  SetTargetToSelf
  SetTargetToTargetLeftHand
    DropItems
    tmpargument = 16
    SendMessageNear
IfDropped
  SetTargetToSelf
  StopTargetMovement
  UnkeepAction
IfUsed
  SetTargetToWhoeverIsHolding
  tmpargument = 25
  SetTargetReloadTime
  IfTargetIsAPlayer
    // =Cuddles=
    SetTargetToSelf
    tmpargument = 7
    SendMessageNear

    // Level
    tmpx = targetlevel
    tmpy = 2
    IfXIsMoreThanY
      tmpx = 2
    tmpargument = 8 + tmpx
    SendMessageNear

    // Stats
    tmpx = targetlife > 9
    tmpy = 2
    IfXIsMoreThanY
      tmpx = 3
    tmpargument = 11 + tmpx
    tmpx = selfstr > 8
    tmpy = selfwis > 8
    SendMessageNear

    // Stats
    tmpx = selfint > 8
    tmpy = selfdex > 8
    tmpdistance = targetexp
    tmpargument = 15
    SendMessageNear
IfCrushed
  tmpx = selfx
  tmpy = selfy
  tmpdistance = selfz
  tmpargument = 4
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  GoPoof
  tmpargument = 2
  SendMessageNear
IfCleanedUp				// Respawn
  IfTargetIsAlive			  // Make sure killer is away
    tmpx = 1100				    //
    tmpy = targetdistance		    //
    IfXIsLessThanY			    //
      RespawnCharacter			      //
  Else					  // Killer is far away
    RespawnCharacter			    //
IfKilled				// This reduces the height of the char
  IfNameIsKnown
    SetOldTarget
    SetTargetToOwner
    IfTargetIsAlive
      tmpargument = 0
      SendMessageNear
    SetTargetToOldTarget
  Else
    tmpargument = 1
    SendMessageNear
  tmpargument = 30			  // Dead height
  SetBumpHeight
  tmpargument = 5			  // Death Sound
  PlaySound

IfAttacked				// Don't take kindly to attackers
  SetTargetToWhoeverAttacked		  //
    tmpargument = 4			    // Ouch Sound
    PlaySound

IfBumped				// Scooch around
  tmpx = rand & 1023 + targetx - 512
  tmpy = rand & 1023 + targety - 512
  ClearWaypoints
  AddWaypoint

IfTimeOut				// This is done every so often
  // Do croaking
  GetContent
  tmpx = rand % 100
  tmpy = 83
  IfXIsMoreThanY
    tmpargument = tmpargument - 1
    SetContent
    tmpargument = rand & 1
    PlaySound
    tmpargument = 12
  Else
    tmpx = rand & 15
    tmpy = 7
    IfXIsMoreThanY
      tmpargument = tmpx > 1
      SetContent
  tmpargument = rand & 15 + 30
  SetTime

  IfHeld
    // Don't bother moving around if held
    DoNothing
  Else
    SetTargetToOwner
    IfTargetIsSelf
      tmpx = rand & 1023 - 512 + selfx
      tmpy = rand & 1023 - 512 + selfy
    Else
      SetTargetToNearbyEnemy
        tmpargument = 15
        SetTime
        tmpx = targetdistance
        tmpy = 150
        IfXIsLessThanY
          IfFacingTarget
            // Attack the enemy...
            DropItems
            tmpargument = ACTIONUA	//Bite
            DoAction
        tmpx = targetx
        tmpy = targety
        tmpturn = targetturnto
        tmpdistance = 800
        Compass
      Else       
        // No enemies around, so follow owner
        tmpx = ownerx
        tmpy = ownery
    ClearWaypoints			  //
    AddWaypoint				  //
    
End					// Finished with this character
