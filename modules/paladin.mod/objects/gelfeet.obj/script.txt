//------------------------------------------------------------------------------
IfHealed
  // Damage is inverted to make the Gelfeet invincible...
  tmpargument = 0
  PlaySound

  // Drop the item it's carrying
  tmpargument = ACTIONHA
  DoAction
  DropItems

IfTimeOut
  tmpargument = 100
  SetTime
  SetTargetToRider
    IfTargetIsOnHatedTeam
      // Hurt the character
      tmpargument = 512
      DamageTarget

      // Corrode items
      tmpx = rand & 1
      tmpy = 0
      IfXIsEqualToY
        SetTargetToTargetLeftHand
          tmpargument = [RUST]
          IfTargetHasVulnerabilityID
            tmpargument = 0
            SendMessageNear
            PoofTarget
      Else
        SetTargetToTargetRightHand
          tmpargument = [RUST]
          IfTargetHasVulnerabilityID
            tmpargument = 0
            SendMessageNear
            PoofTarget
    Else
      // Corrode items
      tmpargument = [RUST]
      IfTargetHasVulnerabilityID
        tmpargument = 0
        SendMessageNear
        PoofTarget

IfBumped				// Eat the enemy
  SetTargetToWhoeverBumped
  tmpx = targetz
  tmpy = selfz + 30
  IfXIsLessThanY
    IfFacingTarget
      tmpargument = ACTIONME
      DoAction
      tmpargument = 0
      PlaySound
    
IfSpawned				// This is done once in a lifetime
  ClearWaypoints			  // Now go there
  tmpx = 10*128				  // Waypoint 0
  tmpy = 57*128				  //
  AddWaypoint				  //
  tmpx = 10*128				  // Waypoint 1
  tmpy = 54*128				  //
  AddWaypoint				  //
  tmpx = 13*128				  // Waypoint 2
  tmpy = 54*128				  //
  AddWaypoint				  //
  tmpx = 13*128				  // Waypoint 3
  tmpy = 57*128				  //
  AddWaypoint				  //
End					// Finished with this character
