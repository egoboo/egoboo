//Look very angry, because we really are...
IfSpawned
  tmpargument = ACTIONMC
  DoAction
  tmpargument = 1
  PlayFullSound
  
IfTimeOut				// This is done every so often
  
  //Groan
  tmpx = rand & 255
  tmpy = 245
  IfXIsMoreThanY
    tmpargument = rand & 1 + 1
    PlaySound
  
  //Follow the tourist!
  SetTargetToNearestEnemy		  
  IfTargetKilled
    tmpx = selfspawnx     //No enemy... return home
    tmpy = selfspawny
  Else
    tmpx = targetx
    tmpy = targety
    tmpdistance = 500
    tmpturn = targetturnto
    Compass
  ClearWaypoints
  AddWaypoint
  tmpargument = rand & 15 + 10		  // Wait a while and try again
  SetTime

  //Attack
  IfTargetIsOnHatedTeam
    IfFacingTarget
      tmpx = targetdistance
      tmpy = 200
      IfXIsLessThanY
        tmpargument = ACTIONUA
        DoAction

//Don't get stuck in other objects such as braziers or chests
IfBumped
  SetTargetToWhoeverBumped
  IfTargetIsOnHatedTeam
    DoNothing
  Else
    tmpx = rand & 511 + targetx - 256
    tmpy = rand & 511 + targety - 256
    ClearWaypoints
    AddWaypoint
  
End					// Finished with this character
